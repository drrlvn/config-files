# spatz prompt theme
# vim: set ft=zsh:

autoload -Uz vcs_info

zstyle ':vcs_info:*' enable git hg svn cvs
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr		'%F{green}●'
zstyle ':vcs_info:*' unstagedstr	'%F{red}⚡'
zstyle ':vcs_info:*' branchformat	'%b%%b%f:%B%F{yellow}%r'
zstyle ':vcs_info:*' formats		'%F{magenta}(%F{white}%b%c%u%m%F{magenta}) '
zstyle ':vcs_info:*' actionformats	'%F{magenta}(%F{white}%b%c%u%m%F{magenta}|%F{red}%a%F{magenta}) '
zstyle ':vcs_info:*+set-message:*' hooks set-message

+vi-set-message() {
	case ${vcs} in
		git)
			_prompt_char='%F{green}±'

			# Show +N/-N when your local branch is ahead-of or behind remote HEAD.
			local ahead behind
			local -a gitstatus

			ahead=$(git rev-list ${hook_com[branch]}@{upstream}..HEAD 2>/dev/null | wc -l)
			(( $ahead )) && gitstatus+=( "%F{green}+${ahead}" )

			behind=$(git rev-list HEAD..${hook_com[branch]}@{upstream} 2>/dev/null | wc -l)
			(( $behind )) && gitstatus+=( "%F{red}-${behind}" )

			if (( ${#gitstatus} )); then
				hook_com[misc]+="%F{magenta}|%b"${(j:%F{white}/:)gitstatus}"%B"
			fi
			;;

		hg)
			_prompt_char='%F{black}☿'
			;;

		svn)
			_prompt_char='%F{blue}⚡'
			;;

		cvs)
			_prompt_char='%F{white}c'
			;;
	esac
}

precmd() {
	_prompt_char='%#'
	vcs_info
}

PROMPT='%B%(!.%F{red}%m.%F{green}%n@%m) %(?.%F{cyan}:%).%F{red}:()%F{blue} ${_prompt_char}%b%f%k '
RPROMPT='%B${vcs_info_msg_0_}%F{blue}(%~)'
