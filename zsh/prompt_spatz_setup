# spatz prompt theme
# vim: set ft=zsh:

autoload -Uz vcs_info

zstyle ':vcs_info:*' enable git hg svn cvs
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr		'%F{green}●'
zstyle ':vcs_info:*' unstagedstr	'%F{red}⚡'
zstyle ':vcs_info:*' branchformat	'%b%%b%f:%B%F{yellow}%r'
zstyle ':vcs_info:*' formats		'%F{magenta}(%F{white}%b%c%u%m%F{magenta}) '
zstyle ':vcs_info:*' actionformats	'%F{magenta}(%F{white}%b%c%u%m%F{magenta}|%F{red}%a%F{magenta}) '
zstyle ':vcs_info:*+set-message:*' hooks set-message

typeset -gA vcs_prompt_chars
vcs_prompt_chars=(
    'git' '%F{red}± '
    'hg'  '%F{black}☿ '
    'svn' '%F{blue}⚡ '
    'cvs' '%F{white}c '
)

+vi-set-message() {
    _prompt_char=$vcs_prompt_chars[$vcs]

    case ${vcs} in
        git)
            # Show +N/-N when your local branch is ahead-of or behind remote HEAD.
            local ahead behind
            local -a gitstatus

            ahead=$(git rev-list ${hook_com[branch]}@{upstream}..HEAD 2>/dev/null | wc -l)
            (( $ahead )) && gitstatus+=( "%F{green}+${ahead}" )

            behind=$(git rev-list HEAD..${hook_com[branch]}@{upstream} 2>/dev/null | wc -l)
            (( $behind )) && gitstatus+=( "%F{red}-${behind}" )

            if (( ${#gitstatus} )); then
                hook_com[misc]+="%F{magenta}|%b"${(j:%F{white}/:)gitstatus}"%B"
            fi
            ;;

    esac
}

precmd() {
    _prompt_char=''
    vcs_info
}

if [[ -n $SSH_CLIENT || $EUID -eq 0 ]]; then
    PROMPT_HOST='%F{red}%n@%m%f --'
fi
PROMPT="%B${PROMPT_HOST} \${_prompt_char}%(?.%F{cyan}:%).%F{red}:() %F{red}❯%F{yellow}❯%F{green}❯%b%f%k "
RPROMPT='%B${vcs_info_msg_0_}%F{blue}(%~)'
